COMMENT ⊗   VALID 00007 PAGES
C REC  PAGE   DESCRIPTION
C00001 00001
C00002 00002	BEGIN TVSER ↔ SUBTTL	TV CAMERA SERVICE ROUTINE - J.SAUTER, 1/21/67
C00003 00003	IFE FTDISK,<
C00006 00004	IFN FTDISK,<
C00007 00005	TVINP:	XCTR	XR,[HRRZ AC1,(UUO)]	ADDRESS CHECK THE COMMAND BLOCK
C00009 00006	↑TVRED:	MOVEI	DDB,TVDDB			HERE FROM FBPACK
C00012 00007	
C00013 ENDMK
C⊗;
BEGIN TVSER ↔ SUBTTL	TV CAMERA SERVICE ROUTINE - J.SAUTER, 1/21/67

TVINI:	CONO	IOP,0		;STOP THE 167.
	CONO	404,0		;AND THE CAMERA
	POPJ	P,		;AND RETURN.

IFE FTDISK,<

	JRST	TVINI		;INITIALIZE
	JRST	HUNG		;HUNG: PRINT MESSAGE & STOP JOB.
↑TVDSP:	JRST	TVINI		;RELEASE
	JRST	TVINI		;CLOSE
	JSP	DAT,UERROR	;INPUT
	JSP	DAT,UERROR	;OUTPUT
	JRST	CPOPJ1		;ENTER
	JRST	CPOPJ1		;LOOKUP
	JSP	DAT,UERROR	;DUMP OUTPUT
	JRST	TVIN		;DUMP INPUT
	POPJ	P,		;SETO
	POPJ	P,		;SETI
	POPJ	P,		;GETF
	JRST	CPOPJ1		;RENAME
	POPJ	P,		;CLOSE INPUT
	POPJ	P,		;DIRECTORY CLEAR
	POPJ	P,		;MTAPE

TVIN:	HRRZ	TAC,UUO
	CAMLE	TAC,USRREL	;IS ADDRESS LEGAL?
	JRST	ADRERR		;NO.
	HRRZ	TAC,@UUO	;GET FIRST WORD 
	HLRO	DAT,@UUO
	SUB	TAC,DAT		;CALC FINAL ADDRESS
	CAMLE	TAC,USRREL	;IN BOUNDS?
	JRST	ADRERR		;NO.
	MOVEI	TAC,IOPANY!IOPJDN;YES. DO HIS OP FOR HIM.
	HRRM	TAC,TVINT
	MOVE	TAC,@UUO
	ADDI	TAC,0(PROG)	;MAKE ABS ADDR.
	MOVEM	TAC,TVDAT	;SAVE FOR LATER
	MOVEI	TAC,@UUO	;GET ABS ADDR OF DATA HEADER.
	MOVE	TAC1,1(TAC)	;CONO TO 404.
	HRRM	TAC1,CON404	;SAVE IT.
	MOVEI	TAC1,2(TAC)	;DATAO TO 404
	HRRM	TAC1,DAT404	;SAVE IT TOO.
	ADDI	TAC,3
	HRRM	TAC,TVCONI	;SET UP THE CONI BACK TO USERS CORE
	PUSHJ	P,SETACT
	MOVE	TAC,[CLKDES,,1]
	CONO	PI,PIOFF
	IDPB	TAC,CLOCK
	CONO	PI,PION
	POPJ	P,		;RETURN TO UUOCON.

CLKDES:	CONO	PI,PIOFF	;TURN OFF PI SYSTEM WHILE WE FIDDLE
	CONO	IOP,TVCHN
	DATAO	IOP,TVDAT
CON404:	CONO	404,0		;FIXED UP.
DAT404:	DATAO	404,0		;ALSO FIXED UP.
	CONO	PI,PION		;NOW LET IT FLY!
	POPJ	P,		;RETURN TO CLOCK.

↑TVINT:	JSR	TVSAV		;SAVE ACS
TVCONI:	CONI	IOP,.		;GETS FIXED UP.
HUNG:	PUSHJ	P,TVINI		;SHUT OFF THE 167 AND CAMERA
	MOVEI	DDB,TVDDB
	MOVE	IOS,DEVIOS(DDB)
	TLZE	IOS,IOW
	PUSHJ	P,SETIOD	;SET I/O DONE.
	JRST	CLRACT		;CLEAR I/O ACTIVE BIT, RESTORE AC'S & DISMISS

TVDAT:	0
>;END IFE FTDISK
IFN FTDISK,<
	JRST	TVINI		;INITIALIZE
	JRST	HUNG		;HUNG TV CAMERA
↑TVDSP:	POPJ	P,		;RELEASE.
	POPJ	P,		;CLOSE OUTPUT
	JSP	DAT,UERROR	;OUTPUT
	JSP	DAT,UERROR	;INPUT
	JRST	CPOPJ1		;ENTER
	JRST	CPOPJ1		;LOOKUP
	JSP	DAT,UERROR	;DUMP OUTPUT
	JRST	TVINP		;DUMP INPUT
	POPJ	P,		;USETO
	POPJ	P,		;USETI
	POPJ	P,		;UGETF
	JRST	CPOPJ1		;RENAME
	POPJ	P,		;CLOSE INPUT
	POPJ	P,		;UTPCLR
	POPJ	P,		;MTAPE
TVINP:	XCTR	XR,[HRRZ AC1,(UUO)]	;ADDRESS CHECK THE COMMAND BLOCK
	XCTR	XR,[MOVE TAC1,3(UUO)]	;ADDRESS CHECK THE END.
	XCTR	XR,[MOVE TAC1,(AC1)]	;ADDRESS CHECK FIRST DATA LOCATION.
	XCTR	XR,[HLRO TAC1,(UUO)]	;GET -SIZE OF TRANSFER
	CAIG	AC1,JOBPFI		;ABOVE AREA PROTECTED FROM I/O?
	JRST	UADRER			;NO. ADDRESS ERROR.
	SUB	AC1,TAC1		;CALCULATE THE END OF THE TRANSFER.+1
					;ADDRESS CHECK IS DONE BELOW.
	SETOM	DSKCNT(DDB)		;FLAG WE ARE THE TV CAMERA (SEE DSKSER)
	HLRZ	TAC1,PROG		;GET LOWERS PROTECTION.
	CAIL	TAC1,(UUO)		;MAKE SURE COMMAND BLOCK IS IN LOWER
	CAMGE	TAC1,AC1		;MAKE SURE DATA IS IN THE LOWER TOO.
	JRST	UADRER			;ADDRESS ERROR.
	MOVEI	TAC,@UUO		;GET ADDRESS OF COMMAND BLOCK
	MOVEM	TAC,DSKBUF(DDB)		;STORE ADDRESS OF COMMANDS. FOR TVRED
	HRLZ	TAC1,DDB	;SET UP FOR MONITOR QUEUE
	TLO	TAC1,400000	;FLAG AS INPUT.
	PUSHJ	P,SETACT	;YES, LOCK INTO CORE FIRST
	JRST	FBTVIN		;THEN GO TO FAST-BAND SERVICE FOR QUEUEING
↑TVRED:	MOVEI	DDB,TVDDB			;HERE FROM FBPACK
	MOVE	TAC1,DSKBUF(DDB)

;HERE FROM MONGO WITH IOP OURS AND ADDRESS OF USERS 4
;WORD COMMAND BLOCK IN TAC1.  WE MAY BE AT UUO LEVEL
;IF MONITOR BUFFER WAS EMPTY WHEN WE CALLED TVIN, OR WE
;MAY BE AT INTERRUPT LEVEL, IF WE GOT HERE THROUGH THE
;SUCCESSFUL COMPLETION OF THE PREVIOUS DISK OP.

	LDB	J,PJOBN
	HRRZ	J,JBTADR(J)	;RELOCATION
	ADD	J,0(TAC1)	;RELOCATE CORE ADDRESS
	MOVEI	TAC,IOPANY!IOPJDN;CONSO BITS FOR IOP.
	CONO	PI,DSKOFF	;JUST IN CASE.
	CONO	IOP,TVCHN	;GIVE IOP ITS CHANNEL AND
				;SET IT UP FOR INPUT.
	DATAO	IOP,J		;FEED IT WC,MA (ABSOLUTE).
	CONO	TV,@1(TAC1)	;SEE TV INTERFACE WRITEUP.
	DATAO	TV,2(TAC1)
	HRRM	TAC,TVCON
	CONO	PI,DSKON
	POPJ	P,

HUNG:	AOSA (P)		;SET SKIP RETURN SO JOB WON'T BE KILLED.
↑TVINT:	JSR	TVSAV		;SAVE ACS, SET UP PDL.
TVIN1:	MOVEI	DDB,TVDDB
	MOVE	TAC,DSKBUF(DDB)
	CONI	IOP,3(TAC)	;GIVE USER IOP STATUS
	MOVE	TAC1,3(TAC)	;GET STATUS FOR US TOO.
	MOVE	IOS,DEVIOS(DDB)	;GET IOS
	TRNE	TAC1,IOPANY	;ANY IOP ERROR BITS?
	TRNN	IOS,100		;ENABLED FOR THIS DATA?
	JRST	TVIN2		;NO ERRORS OR NOT ENABLED FOR ERRORS
	LDB	J,PJOBN
	DATAI	IOP,IOS		;GET THE IOP WCMA
	HRRZ	IOS,IOS
	HRRZ	TAC1,JBTADR(J)	;GET THE USER'S ADDRESS
	SUB	IOS,TAC1	;USER'S RELATIVE ADDRESS. 
	HRLM	IOS,3(TAC)	;GIVE USER HIS ADDRESS.
TVIN2:	CONO	IOP,0
	HLLZS	TVCON
	MOVEI	IOS,IOACT	; FIRST, CLEAR THE ACTIVE BIT
	ANDCAB	IOS,DEVIOS(DDB)
	TLNE	IOS,IOW		; NOW, BRING HIM OUT OF IO WAIT
	PUSHJ	PDP,SETIOD
	JRST	FBDFINT		; NOW TO FAST-BAND SERVICE TO BE DEQUEUED
>
	BEND TVSER
