COMMENT ⊗   VALID 00005 PAGES
C REC  PAGE   DESCRIPTION
C00001 00001
C00002 00002	BEGIN PTRSER ↔ SUBTTL PTRSER	PAPER TAPE READER SERVICE, J. SAUTER, 11-NOV-68
C00003 00003	UUO DISPATCH TABLE:
C00009 00004	PAPER TAPE READER INTERRUPT.
C00013 00005	P1 CLOCK LEVEL AND HUNG TIMEOUT CODE
C00019 ENDMK
C⊗;
BEGIN PTRSER ↔ SUBTTL PTRSER	PAPER TAPE READER SERVICE, J. SAUTER, 11-NOV-68

;PTR CONTROL REGISTER
POWER←←400		;POWER ON ON PDP-6, NOT EOT ON PDP-10.
BINARY←←40		;BINARY MODE.
BUSY←←20		;PTR IS BUSY READING A WORD.
DONE←←10		;PTR HAS A WORD.

;SPECIAL ASCII CHARACTERS:
NULL←←200		;NULL CHARACTER
RUBOUT←←377		;RUBOUT.

;SPECIAL BITS IN LH OF IOS:
PTRPOW←←200000		;PTR HAS (HAD) POWER ON.
PTRNPW←←100000		;PTR DOESN'T HAVE POWER ON FOR FIRST OPERATION.
STOPTR←←40000		;HUNG TIMEOUT ASSUMES EOF.
;UUO DISPATCH TABLE:
	JRST	PTRINI		;INITIALIZE.
	JRST	PTRHNG		;HUNG  TIMEOUT.
↑PTRDSP:
	JRST	PTRXRL		;RELEASE.
	POPJ	P,		;CLOSE.
	JRST	ILLOUT		;OUTPUT

;INPUT UUO
PTRINP:	PSYNC	PTRCSC
	PUSHJ	P,SETACT	;SET IOACT.
	TLNN	IOS,IOBEG	;FIRST TIME?
	JRST	@PTRIN1		;NO.
	TLO	IOS,IOFST	;YES.  SET UP THINGS.
	TLZ	IOS,PTRPOW
	PUSHJ	P,SETBYT	;SET UP TAC.
	MOVEM	TAC,PTRPTR	;SAVE FOR INTERRUPT ROUTINES.
PTRIN5:	TLZN	IOS,PTRNPW	;HAS THE LOSER FLAG BEEN SEEN ALREADY?
	JRST	@PTRIN1		;TO PTRP1U FOR 1 PROCESSOR, OR PTRP2U FOR TWO.
;HERE ON SECOND INPUT AFTER FIRST INPUT DISCOVERED POWER OFF ON THE PDP-6 READER
	PUSHJ	P,CLRACT	;CLEAR IO ACTIVE
	XSYNC	PTRCSC		;RELEASE SYNCHRONIZER.
	PUSHJ	P,HNGSTP	;PRINT DEVICE PTR OK? AND STOP JOB.
	JRST	PTRINP

PTRP2U:	PUSHJ	P,STOIOS	;STORE IOS.
	SETOM	PTRACT		;REQUEST P2 TO START READER.
	JRST	XPOPJ		;DESYNCHRONIZE AND EXIT

;HERE AT P2 CLOCK LEVEL
↑PTRP2:	MOVE	TAC,PTRACT	;P2 START REQUEST?
	JUMPE	TAC,CPOPJ	;NO.
	PSYNC	PTRCSC		;SYNCHRONIZE.
	SETZM	PTRACT		;INDICATE REQUEST GRANTED.
	MOVEI	DDB,PTRDDB	;SET UP DDB AND IOS.
	MOVE	IOS,DEVIOS(DDB)
	TLZE	IOS,STOPTR	;
	JRST	PTRYRL		;HUNG TIMEOUT REQUESTS THAT THE READER STOP.
PTRP1U:
PTRIN2:	TLZN	IOS,IOBEG		;FIRST TIME?
	JRST	PTRIN3			;NO.
	CONSZ	PTR,POWER		;YES.  POWER ON?
	TLOA	IOS,PTRPOW		;YES.
	JUMPN	PID,PTRIN4		;POWER OFF ON PDP-6 READER.
	SKIPA	TAC1,[PTRCHN!BUSY]	;SET TAC1 WITH CONO START
PTRIN3:	MOVEI	TAC1,PTRCHN!DONE	;SET TAC1 WITH CONO CONTINUE
	PUSHJ	P,STOIOS		;STORE IOS, RESET HUNG COUNT.
	LDB	TAC,PIOMOD		;GET MODE.
	CAIE	TAC,IB			;IMAGE BINARY?
	CAIN	TAC,B			;OR BINARY?
	TRO	TAC1,BINARY		;YES.  SET READER INTO BINARY MODE.
	MOVEM	TAC1,PTRSTR		;REMEMBER FOR INTERRUPT LEVEL.
	MOVEI	TAC,DONE		;WAIT FOR DONE FLAG.
	CONO	PI,PTROFF
	SETZM	PTRDSC			;SET NOT PTR DISCONNECTING.
	HRRM	TAC,@PTRCON		;SET UP BITS.
	CONSO	PTR,7			;SKIP IF READER IS RUNNING!
	CONO	PTR,@PTRSTR		;READER IDLE.  KICK IT.
	CONO	PI,PTRON
	CONSZ	PTR,POWER		;WE SHOULD SEE POWER OR TAPE BY NOW.
	JRST	XPOPJ			;DESYNCRONIZE AND RETURN.
PTRIN4:	TLO	IOS,PTRNPW!IOBEG	;FLAG NO POWER AVAILABLE
	TLZN	IOS,STOPTR
	SETOM	PTRADV			;FLAG THE PDP-10
	PUSHJ	P,STOIOS		;STORE IOS
	JUMPN	PID,XPOPJ
	JRST	PTRIN5			;HANG ON THE PDP-10

PTRINI:	SYNINI	PTRCSC		;INITIALIZE SYNCHRONIZER CELLS.
;DETERMINE WHICH PROCESSOR TO USE.  IF ONE PROCESSOR IS A
;PDP-6, USE IT.  IF P2 DOES NOT EXIST, USE P1, OTHERWISE,
;USE P2.
	MOVEI	AC1,PTRP1U	;ASSUME P1
	MOVEI	AC2,PTRCN1
	HRRZ	TAC,P2NUM	;GET NUMBER OF P2 (=6 OR =10)
	CAIN	TAC,6		;IS IT A PDP-6?
	JRST	USEP2		;YES. USE IT.
	JUMPE	TAC,USEP1	;IF THERE IS NO P2, USE P1.
	HRRZ	TAC,P1NUM	;GET NUMBER OF P1.
	CAIN	TAC,6		;IS IT A PDP-6?
	JRST	USEP1		;YES. USE IT.
USEP2:	MOVEI	AC1,PTRP2U
	MOVEI	AC2,PTRCN2
USEP1:	MOVEM	AC1,PTRIN1	;SET DISPATCH FOR INPUT UUO'S
	MOVEM	AC2,PTRCON	;SET ADDRESS FOR CONSZ BITS.
	HLLZS	PTRCN1
	HLLZS	PTRCN2

PTRXRL:	SETZM	PTRDDB+DEVIOS	;CLEAR READER STATUS.  RELEASE UUO.
↑PTRREL:			;ALSO CALLED FROM SYSINI
	CONO	PTR,0		;STOP READER, DISCONNECT IT FROM PI
	HLLZS	@PTRCON
	POPJ	P,

PTRYRL:	PUSHJ	P,IOSET
	JRST	PWROFF		;SIMULATE POWER OFF.
;PAPER TAPE READER INTERRUPT.
↑PTRINT:
	SKIPL	PTRDSC		;SKIP DATAI IF WE ARE DISCONNECTING.
	DATAI	PTR,PTRCHR	;READ CHAR FROM READER.
	JSR	@PTRSAV		;SAVE ACCUMULATORS.
	PSYNC	PTRCSC
	MOVEI	DDB,PTRDDB	;SET UP REGISTERS
	PUSHJ	P,IOSET
	MOVE	IOS,DEVIOS(DDB)
	CONSO	PTR,POWER	;POWER ON?
	JRST	PWROFF		;NO.
	TLON	IOS,PTRPOW	;POWER IS ON NOW, WAS IT BEFORE?
	JRST	PTRXIT		;POWER IS ON FOR THE FIRST TIME.
	SKIPE	PTRDSC		;IS PTR BEING DISCONNECTED?
	JRST	PTRDIS		;YES. DO IT.
	MOVE	DAT,PTRCHR	;GET CHARACTER.
PTRIN0:	TRNE	IOS,B		;BINARY MODE?
	JRST	PTRI0		;YES. (MODES 10,13,14)
	IORI	DAT,200		;IGNORE PARITY.
	CAIE	DAT,NULL	;NULL CHARACTER?
	CAIN	DAT,RUBOUT	;OR RUBOUT?
	JRST	PTRXIT		;YES.  IGNORE CHAR AND START READING NEXT.
PTRI0:	PUSHJ	P,STODAT	;STORE DATA IN BUFFER.
	JFCL			;CHECKSUM ERROR.
	JRST	PTRI1		;BLOCK COMPLETE.
	MOVEM	ITEM,DEVCTR(DDB);INCOMPLETE.  STORE ITEM.
PTRXIT:
PTRXT1:	PUSHJ	P,STOIOS	; STORE IOS, RESET HUNG COUNT
XPOPJ:	XSYNC	PTRCSC
	POPJ	P,		;DISMISS INTERRUPT.

;MORE I-LEVEL ROUTINES.
PWROFF:	TLZ	IOS,PTRPOW	;NOTE THAT READER IS OFF.
	TLO	IOS,IOEND+IOBEG
	PUSHJ	P,PTRREL	;SHUT IT OFF!
	LDB	TAC,PIOMOD	;GET MODE.
	CAIE	TAC,B		;CHECKSUM-BINARY?
	JRST	PTREI		;NO.
	TLNN	IOS,IOFST	;INCOMPLETE BLOCK?
	TRO	IOS,IOIMPM	;YES. COMPLAIN.
	PUSHJ	P,CLRACT	;CLEAR I/O ACTIVE BIT.
	JRST	PTRI1A		

;BLOCK INCOMPLETE
PTREI:	PUSHJ	P,STOSQD	;TRUNCATE BUFFER, STORE W.C.
	JFCL			;FALL INTO PTRI1
;BLOCK COMPLETE.
PTRI1:	MOVEM	ITEM,DEVCTR(DDB);STORE ITEM COUNT.
PTRI1A:	JUMPE	PID,PTRI2	;NO. ONE PROCESSOR
	SETOM	PTRDSC		;MUST DISCONNECT PTR
	TLZN	IOS,STOPTR
	SETOM	PTRADV		;REQUEST BUFFER ADVANCE.
	JRST	PTRXT1		;RELEASE CONTROL OF IOS AND DISMISS

PTRI2:	TLO	IOS,IOFST	;(SINGLE PROCESSOR)
	TLZE	IOS,IOW		;UNWAIT THE JOB.
	PUSHJ	P,SETIOD	;IF NEEDED.
	TLNE	IOS,PTRPOW	;POWER OFF?
	PUSHJ	P,ADVBFF	;NOT POWER OFF, ADVANCE BUFFERS
	JRST	PTRI3		;NO MORE BUFFER SPACE.
	JRST	PTRXIT		;START READER, STORE IOS, AND  DISMISS INTERRUPT.

PTRI3:	PUSHJ	P,CLRACT	;NO BUFFER SPACE. CLEAR IOACT.
	SETOM	PTRDSC		;REQUST PTR DISCONNECT.
	JRST	PTRXT1		;RELEASE SYNCHRONIZER. DISMISS INTERRUPT.

PTRDIS:	JUMPN	PID,PTRDS1
	SKIPG	@PTRADR		;BUFFER FREE?
	JRST	PTRDS1		;NO. MUST DISCONNECT.
	DATAI	PTR,DAT
	JRST	PTRIN0		;INPUT CHARACTER AND PROCESS AS NORMAL.

PTRDS1:	PUSHJ	P,PTRREL	;STOP PTR.
	JRST	PTRXT1		;DISMISS INTERRUPT
;P1 CLOCK LEVEL AND HUNG TIMEOUT CODE

PTRHNG:	MOVE	TAC,PTRIN1
	CAIN	TAC,PTRP1U
	POPJ	P,			;ON PDP-10 I DON'T UNDERSTAND HANGS.
	PSYNC	PTRCSC			;SYNCHRONIZE.
	MOVE	IOS,PTRDDB+DEVIOS	;GET IOS
	TLZN	IOS,PTRPOW		;HAVE WE SEEN POWER ON?
	JRST	XPOPJ			;NO. LET IT HANG.
	TLON	IOS,STOPTR!IOEND	;MAKE PTR STOP. PRETEND END OF TAPE.
	AOS	(P)			;SET SKIP RETURN TO AVOID HUNG PTR MESSAGE
	MOVEM	IOS,PTRDDB+DEVIOS	;STORE IOS
	SETOM	PTRACT			;KICK PDP-6
	JRST	XPOPJ			;RELEASE SYNCHRONIZER AND WAIT FOR PDP-6.


;HERE AT P1 CLOCK LEVEL
↑PTRP1:	SETZM	PTRADV		;CLEAR REQUEST
	PSYNC	PTRCSC
	MOVEI	DDB,PTRDDB	;LOAD UP REGISTERS.
	MOVE	IOS,DEVIOS(DDB)
	TLNE	IOS,PTRNPW
	TRO	IOS,IOIMPM	;READING WITHOUT READER ON.
	TLO	IOS,IOFST	;NEXT ITEM WILL BE FIRST OF A BUFFER.
	TLZE	IOS,IOW		;IS JOB WAITING FOR PTR?
	PUSHJ	P,SETIOD	;YES.  UNWAIT IT.
	LDB	J,PJOBN		;SET J
	MOVE	PROG,JBTADR(J)	;
	PUSHJ	P,ADVBFF	;ADVANCE BUFFERS.
	JRST	PTRP1A		;OUT OF BUFFER SPACE.
	TLNE	IOS,IOEND!PTRNPW ;END OF FILE OR ERROR?
	JRST	PTRP1A		;YES. STOP READER.
	PUSHJ	P,STOIOS	;STORE IOS.
	SETOM	PTRACT		;ACTIVATE PTR.
	JRST	XPOPJ		;RELEASE SYNCHRONIZER AND RETURN

PTRP1A:	PUSHJ	P,CLRACT	;CLEAR IOACTIVE, DON'T RESTART THE READER
	JRST	XPOPJ

	BEND	PTRSER
