COMMENT ⊗   VALID 00004 PAGES
C REC  PAGE   DESCRIPTION
C00001 00001
C00002 00002	BEGIN  MAIL ↔	SUBTTL MAIL SERVICE.
C00005 00003	WRCV:	PUSHJ	P,SRCV
C00006 00004	 ROUTINE TO GET THE JOB NUMBER OF THE ADDRESSEE
C00008 ENDMK
C⊗;
BEGIN  MAIL ↔	SUBTTL MAIL SERVICE.

↑UMAIL:	CAILE	UCHN,MAXMAIL		;AC FIELD IN BOUNDS?
	JRST	UUOERR			;NO LOSE.
	HRRZ	AC1,UUO			;GET THE EFFECTIVE ADDRESS
	CAML	AC1,USRREL(PID)		;IS THIS INSIDE OF USER'S MAXIMUM?
	JRST	UUOERR			;NO LOSE.
	JRST	@MALTBL(UCHN)		;DISPATCH TO APPROPRIATE ROUTINE

DEFINE UUOS{FOR X IN(SEND,WRCV,SRCV,SKPME,SKPHIM,SKPSEN)}

UUOMAC MALTBL,MAIL
MAXMAIL←←UUOCNT-1

SKPSEN:	JSP	AC3,GETJOB		;THIS JSP MUST BE LABELED SKPSEN
	JRST	SEND1			;ADDRESSEE EXISTS, BEHAVE LIKE "SEND" UUO
SEND:	JSP	AC3,GETJOB		;DAT ← JOB# OF RECEIVING JOB
SEND1:	SKIPE	JBTMAIL(DAT)		;DOES THIS GUY HAVE MAIL ALREADY?
	POPJ	P,			;YES. CAN'T DO IT.
	XCTR	XR,[HRRZ TAC,1(UUO)]	;GET THE ADDRESS OF THE MAIL BLOCK
	MOVEI	AC1,37(TAC)		;GET ADDRESS OF LAST WORD.
	CAMLE	AC1,USRREL(PID)		;GOD FORBID HE SHOULD SEND MAIL FROM UPPER.
	JRST	ADRERR
	MOVEI	AC3,40
	PUSHJ	P,UFSGET
	HRRZM	AC1,JBTMAIL(DAT)
	ADDI	TAC,(PROG)
	HRL	AC1,TAC
	MOVE	AC2,AC1
	BLT	AC1,37(AC2)
	MOVEI	J,(DAT)	
	MOVM	TAC,JOBQUE(J)		; WAKE HIM UP IF HE IS IN IOWQ
	SKIPG	JBTSTS(J)		; IF NOT RUNNABLE, DON'T REQUE HIM
	CAIE	TAC,IOWQ
	JRST	NOWAKE
	MOVEI	TAC,RUNQ
	MOVNM	TAC,JOBQUE(J)
	PUSHJ	P,REQUE
NOWAKE:	MOVSI	AC1,INTMAIL		; IS HE ENABLED FOR A MAIL INTERRUPT?
	AOS	(P)			;SET UP SUCCESS RETURN FROM SEND
	TDNN	AC1,JBTIEN(J)
	POPJ	P,			;NO RETURN QUICK
	IORM	AC1,JBTIRQ(J)		; YES, GIVE HIM THE INTERRUPT.
	SETOM	INTREQ(PID)
	JRST	WSCHED			;SHUT US OFF TO GET THE INTERRUPT PROCESSED.
WRCV:	PUSHJ	P,SRCV
	TROA	AC1,IOWQ
	POPJ	P,
	MOVNM	AC1,JOBQUE(J)
	PUSHJ	P,WSCHED
	JRST	WRCV

SRCV:	SKIPN	AC1,JBTMAIL(J)
	POPJ	P,
	SETZM	JBTMAIL(J)
	HRL	TAC,AC1
	HRRI 	TAC,@UUO
	MOVEI	TAC1,37(TAC)
	BLT	TAC,(TAC1)
	PUSHJ	P,FSGIVE
	JRST	CPOPJ1

SKPME:	SKIPA	DAT,J
SKPHIM:	JSP	AC3,GETJOB
	SKIPN	JBTMAIL(DAT)
	POPJ	P,
	JRST	CPOPJ1

; ROUTINE TO GET THE JOB NUMBER OF THE ADDRESSEE

↑FNDJOB:				;A ROSE BY ANY OTHER NAME, ETC.
GETJOB:	MOVE	DAT,@UUO		;GET USER'S ARGUMENT
	TDNN	DAT,[-1,,777700]	;IS IT A NAME?
	JRST	GJ1			;NO THIS IS A NUMBER
	MOVSI	TAC,-JOBN		;SEARCH ALL JOB NAMES.
	CAME	DAT,JOBNAM(TAC)		;IS THIS ONE?
	AOBJN	TAC,.-1			;NOT A MATCH. LOOP.
	JUMPGE	TAC,GJERR		;JUMP IF NO NAMES
	MOVE	TAC1,TAC		;CONTINUE SCAN. LOOK FOR ANOTHER
	AOBJP	TAC1,GJ2		;TEST AT TOP OF LOOP. JUMP WHEN DONE
	CAME	DAT,JOBNAM(TAC1)	;SAME NAME?
	JRST	.-2			;NO. LOOP.
	MOVEI	TAC,GTGARP		;PICKUP THE AMBIGUOUS NAME ERROR MESSAGE
	JRST	GJERR1			;GO TO ERROR STUFF

GJ2:	MOVEI	DAT,(TAC)		;GET JOB NUMBER.
GJ1:	MOVE	TAC,JBTSTS(DAT)		;GET JOB STATUS
	TLNE	TAC,JNA			;IS THIS JOB SLOT ASSIGNED?
	JRST	(AC3)			;YES. RETURN WITH JOB NUMBER IN DAT
GJERR:	MOVEI	TAC,GTGAR1		;GET ADDRESS OF ERROR ROUTINE
GJERR1:	HRRZ	AC3,AC3			; FIND OUT WHO CALLED GETJOB
	CAIN	AC3,SKPSEN+1		; WAS IT SKPSEN UUO?
	JRST	CPOPJ2			; YES, TERMINATE THE UUO
	PUSHJ	P,HNGMES		; NO, COME BACK WHEN HE TYPES CONTINUE
	JRST	GETJOB			; 

GTGAR1:	ASCIZ/
?NON-EX JOB NAME OR NUMBER
 /

GTGARP:	ASCIZ /
?AMBIGUOUS  JOB NAME
/
BEND
