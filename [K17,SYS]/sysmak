COMMENT ⊗   VALID 00002 PAGES
C REC  PAGE   DESCRIPTION
C00001 00001
C00002 00002	BEGIN SYSMAK -- MAKE NEW SYSTEM FROM OLD.
C00005 ENDMK
C⊗;
BEGIN SYSMAK -- MAKE NEW SYSTEM FROM OLD.
SUBTTL SYSMAK - J. SAUTER, STANFORD, 11-FEB-68, NEW SYSTEMS FOR OLD!

;SYSMAK RUNS IN THE OLD SYSTEM AND CREATES A NEW ONE
;ON TOP OF ITSELF.  WHEN THE SYSTEM IS HALTED AND STARTED
;AT LOCATION 202 (SEE FIRST) CONTROL IS 
;TRANSFERRED TO SYSMAK.  THIS ROUTINE ASSUMES THAT JOB 1
;IS A CORE IMAGE TO RUN IN EXEC MODE.
;IT BLT'S THE PROGRAM DOWN (FROM 140 THROUGH 77777) SO
;THAT IT MAY RUN IN EXEC MODE, AND HALTS.  THE VARIOUS
;INCOMPATABILITIES BETWEEN USER AND EXEC DDT ARE TAKEN
;CARE OF AS WELL.


↑SYSMAK:
	SETZM	41		;TRY TO KILL THE 6
	SETZM	46
	SETZM	SIXRUN
	MOVEI	1,100000
	SOJG	1,.		;GIVE IT A CHANCE TO DIE
	HRRZ	1,JBTADR+1	;GET RELOCATION FOR JOB 1
	JUMPE	1,SYSBUG	;NOT THERE?
	MOVE	2,JOBSYM(1)	;GET HIS SYMBOL TABLE POINTER.
	MOVEM	2,DDTSYM	;PUT WHERE EXEC DDT WILL SEE IT.
	MOVSI	10,(<BLT 3,0>)
	HRR	10,JOBREL(1)
	HRRZM	10,DDTMEM	;SET INITIAL MEMORY LIMIT
	MOVSI	3,140(1)
	HRRI	3,140
	MOVSI	11,(<SETZM>)
	HRRI	11,1(10)	;ZERO FIRST LOC AFTER THE LAST MOVED BY BLT.
	MOVSI	4,1(10)
	HRRI	4,2(10)		;BLT AC
	MOVE	12,RMEMSIZ	;CLEAR ALL OF CORE
	SUBI	12,2		;EXCEPT THE LAST WORD (BLT WITH EA=-1 LOSES)
	HRLI	12,(<BLT 4,0>)
	MOVSI	13,(<JRST 4,>)
	HRR	13,JOBSA(1)	;MAKE 11=HALT STARTING ADDRESS.
	JRST	10		;BLOCK IT DOWN.

SYSBUG:	DATAO	CTY,[7]
	HALT	SYSMAK		;LET HIM TRY AGAIN

	LIT

↑MAKEND:	BEND
