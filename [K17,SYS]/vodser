COMMENT ⊗   VALID 00002 PAGES
C REC  PAGE   DESCRIPTION
C00001 00001
C00002 00002	BEGIN VODSER ↔ SUBTTL VODER SERVICE ROUTINE.
C00005 ENDMK
C⊗;
BEGIN VODSER ↔ SUBTTL VODER SERVICE ROUTINE.
; REG 6-AUG-74
;COMMENT - THIS ASSUMES THAT THE BUFFERS ARE IN LOWER SEGMENT!

IFN VODNUM,<

; VODER PARAMETER ASSIGNMENTS
	↑VODINB←←10		;CONI/CONO = INTERRUPTING-DONE/CLEAR INTERRUPT - LOAD
	VODGO←←20		;CONI/CONO = BUSY/GO

; VODER SERVICE DISPATCH TABLE

	JRST	VODINI		;INITILIZE
	JRST	VODREL		;HUNG TIMEOUT.
↑VODDSP:JRST	VODREL		;RELEASE
	JRST	OUT		;CLOSE - CALL UUOCON'S OUT
	JRST	VODOUT		;OUTPUT
	JRST	ILLINP		;INPUT

VODINI:	CONO	VOD,VODINB	;SET "LOAD MODE", CLEAR INTERRUPTING
	CONO	VOD,VODGO	;SET "GO MODE" - CLEARS ANYTHING LEFT IN VODER
	MOVSI	TAC,1
	CONSZ	VOD,VODGO	;WAIT FOR BUSY TO CLEAR
	SOJG	TAC,.-1		;BUT DON'T WAIT FOREVER...
VODREL:	MOVEI	IOS,0		;CLEAR DEVIOS
VODDSC:	CONO	VOD,0		;CLEAR PIA - ILEVEL RAN OUT OF DATA.
	HLLZS	VODCON		;CLEAR CONSZ CHAIN
	JRST	CLRACT		;CLEAR IOACT, STORE IOS, RETURN

↑VODINT:JSR	VODSAV		;SAVE ACS FOR INTERRUPT
	MOVEI	DDB,VODDDB
	MOVE	IOS,DEVIOS(DDB)
	TLZE	IOS,IOW		;WAS JOB IN IOWAIT?
	PUSHJ	P,SETIOD	;YES.  HE CAN CONTINUE NOW
	PUSHJ	P,IOSET		;SET UP PROG (I THINK THIS IS NECESSARY - REG)
	PUSHJ	P,ADVBFE	;ADVANCE TO NEXT BUFFER-FULL
	JRST	VODDSC		;NONE THERE. DISCONNECT.
VODOUT:	MOVE	TAC,DEVOAD(DDB)	;GET ADDRESS OF CURRENT BUFFER?
	ADDI	TAC,1
	MOVE	ITEM,@TAC	;WORD COUNT. (USES PROG!)
	TLO	TAC,004400	;SET BYTE SIZE.  INDEX OF PROG!
	CONO	VOD,VODINB	;CLEAR INTERRUPT REQUEST AND PIA. SET LOAD MODE
VODIN1:	ILDB	TAC1,TAC
	DATAO	VOD,TAC1
	SOJG	ITEM,VODIN1
	MOVEI	TAC,VODINB
	CONO	PI,VODOFF
	HRRM	TAC,VODCON
	CONO	VOD,VODGO!VODCHN	;SET GO AND PIA.
	CONO	PI,VODON
	TLO	IOS,IO		;SET OUTPUT DIRECTION
	JRST	SETACT		;SET IOACT, CLEAR IOW, SET HUNG COUNTDOWN
				;DISMISS UUO OR INT. WAIT FOR VOD
>;IFN VODNUM
BEND	VODSER
